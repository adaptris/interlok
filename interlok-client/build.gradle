ext {
  componentName='Interlok Core/Client'
  componentDesc="Interlok client API; allows you to programatically submit messages to an Interlok workflow"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  log4j2Version = "2.17.1"
  mockitoVersion = '4.2.0'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
  api project(':interlok-common')
  api ("org.slf4j:slf4j-api:1.7.33")
  testImplementation ('junit:junit:4.13.2')
  // testCompile ("com.adaptris:interlok-stubs:$interlokCoreVersion") { changing= true}
  testImplementation ("org.apache.logging.log4j:log4j-core:$log4j2Version")
  testImplementation ("org.apache.logging.log4j:log4j-1.2-api:$log4j2Version")
  testImplementation ("org.apache.logging.log4j:log4j-api:$log4j2Version")
  testImplementation ("org.mockito:mockito-core:$mockitoVersion")
  testImplementation ("org.mockito:mockito-inline:$mockitoVersion")

  annotationProcessor project(':interlok-core-apt')
  testAnnotationProcessor project(':interlok-core-apt')

  javadoc project(':interlok-core-apt')
}

javadoc {
  configure(options) {
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addBooleanOption "-no-module-directories", true
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption "tagletpath", configurations.javadoc.asPath
    options.tags('apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:','implNote:a:Implementation Note:')
    title= componentName
  }
}


task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

delombok {
  target = delombokTargetDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
		asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.0.5+")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://github.com/adaptris/interlok/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

clean.dependsOn deleteGeneratedFiles
