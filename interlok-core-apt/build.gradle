ext {
  componentName='Interlok Core/Annotations'
  componentDesc="Custom annotations for Interlok"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
}

dependencies {
  api ("com.thoughtworks.xstream:xstream:1.4.20") {  transitive= false }
  api ("jakarta.validation:jakarta.validation-api:2.0.2")
  // compile files("${System.getProperty('java.home')}/../lib/tools.jar")

  testImplementation("junit:junit:4.13.2")
  testImplementation("org.hibernate.validator:hibernate-validator:6.1.7.Final")
  testImplementation("org.glassfish:jakarta.el:3.0.4")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
  configure(options) {
    options.tags('apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:','implNote:a:Implementation Note:')
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addBooleanOption "-no-module-directories", true
    title= componentName
  }
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

delombok {
  target = delombokTargetDir
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
		asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.8.0+")
        properties.appendNode("tags", "annotations")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://github.com/adaptris/interlok/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

clean.dependsOn deleteGeneratedFiles
