import org.apache.tools.ant.taskdefs.condition.Os

ext {
  componentName='Interlok Core/Base'
  componentDesc="The Core Interlok framework component; this is your must have"
  activeMqVersion='5.17.2'
  bouncyCastleVersion='1.70'
  mysqlDriverVersion='8.0.30'
  slf4jVersion = '1.7.36'
  mockitoVersion = '4.7.0'
  jschVersion = '0.2.4'
  mssqlDriverVersion = '9.2.1.jre8'
  derbyDriverVersion = '10.15.2.0'

  verboseTests= project.hasProperty('verboseTests') ? project.getProperty('verboseTests') : "false"
  classesToTest = project.hasProperty("junit.test.classes") ? project.getProperty("junit.test.classes") : "**/*Test*"
  maxThreads = project.hasProperty("junit.threads") ? project.getProperty("junit.threads") : 1
  interlokCommonsOfflineJavadocDir = project.rootDir.getCanonicalPath() + "/interlok-common/build/docs/javadoc"
  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
}

ext.hostname = { ->
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    return System.getenv("COMPUTERNAME")
  }
  return System.getenv("HOSTNAME")
}

def propertyTemplate(dir, filename) {
  def file = new File(dir, filename + "." + hostname())
  if (file.exists()) {
    return file.getName()
  }
  return filename
}

ext.testResourcesDir = { ->
  return "${project.projectDir}/src/test/resources".replaceAll("\\\\", "/")
}

ext.verboseTestLogging = { ->
  return verboseTests.equals("true")
}

configurations {
  antSql{}
}

// In this section you declare the dependencies for your production and test code
dependencies {

  api project(':interlok-core-apt')
  api project(':interlok-common')

  api ("com.thoughtworks.xstream:xstream:1.4.19")
  api ("org.codehaus.jettison:jettison:1.2")
  api ("io.github.classgraph:classgraph:4.8.116")
  api ("net.jodah:expiringmap:0.5.10")
  api ("com.sun.activation:jakarta.activation:1.2.2")
  api ("org.apache.activemq:activemq-client:$activeMqVersion") {
    exclude group: "org.apache.geronimo.specs", module: "geronimo-jms_1.1_spec"
  }

  // api ("org.apache.geronimo.specs:geronimo-jms_2.0_spec:1.0-alpha-2")
  // Note that jakarta.jms-api is now @3.x but that changes the package from javax.jms -> jakarta.jms
  api ("jakarta.jms:jakarta.jms-api:2.0.3")
  api ("org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final")
  // api ("jakarta.persistence:jakarta.persistence-api:2.2.3")
  api ("jakarta.validation:jakarta.validation-api:2.0.2")
  // Transitive from jetty.
  // api ("jakarta.transaction:jakarta.transaction-api:1.3.3")
  api ("org.hibernate.validator:hibernate-validator:6.1.7.Final")
  api ("org.glassfish:javax.el:3.0.1-b12")
  api ("commons-io:commons-io:2.11.0")
  api ("org.apache.commons:commons-lang3:3.12.0")
  api ("commons-net:commons-net:3.8.0")
  api ("org.apache.commons:commons-pool2:2.11.1")
  api ("org.apache.commons:commons-exec:1.3")
  api ("org.apache.commons:commons-text:1.9")
  api ("org.apache.commons:commons-collections4:4.4")
  api ("org.bouncycastle:bcprov-jdk15on:$bouncyCastleVersion")
  api ("org.bouncycastle:bcpkix-jdk15on:$bouncyCastleVersion")
  api ("org.bouncycastle:bcmail-jdk15on:$bouncyCastleVersion")
  api ("com.sun.mail:jakarta.mail:1.6.7")
  api ("org.glassfish.external:opendmk_jmxremote_optional_jar:1.0-b01-ea")
  api ("org.glassfish.external:opendmk_jdmkrt_jar:1.0-b01-ea")
  api ("com.github.mwiede:jsch:$jschVersion")
  api ("org.eclipse.jetty.aggregate:jetty-all:9.4.48.v20220622")
  api ("javax.servlet:javax.servlet-api:4.0.1")
  api ("net.sf.joost:joost:0.9.1")
  api ("org.quartz-scheduler:quartz:2.3.2") {
    exclude group: "com.zaxxer", module: "HikariCP-java7"
  }
  api ("com.zaxxer:HikariCP:5.0.1")
  api ("net.sf.saxon:Saxon-HE:11.4")
  api ("xerces:xercesImpl:2.12.2")
  api ("com.mchange:c3p0:0.9.5.5")
  api ("org.apache-extras.beanshell:bsh:2.0b6")

  api ("org.slf4j:slf4j-api:$slf4jVersion")

  api ("org.apache.derby:derby:$derbyDriverVersion")
  api ("org.apache.derby:derbytools:$derbyDriverVersion")

  testImplementation ("junit:junit:4.13.2")
  testImplementation ("org.apache.activemq:artemis-jms-server:2.24.0")
  testImplementation ("oro:oro:2.0.8")
  testImplementation ("org.slf4j:jcl-over-slf4j:$slf4jVersion")

  testImplementation ("org.awaitility:awaitility:4.2.0")

  testImplementation ("org.apache.activemq:activemq-broker:$activeMqVersion")
  testImplementation ("org.apache.activemq:activemq-jaas:$activeMqVersion")
  testImplementation ("org.apache.activemq:activemq-kahadb-store:$activeMqVersion")
  testImplementation ("org.apache.activemq.protobuf:activemq-protobuf:1.1")

  testImplementation ("org.jruby:jruby-complete:9.3.7.0")
  testImplementation ("org.mockftpserver:MockFtpServer:3.0.0")
  testImplementation ("org.mockito:mockito-core:$mockitoVersion")
  testImplementation ("org.mockito:mockito-inline:$mockitoVersion")
  testImplementation ("net.sourceforge.jtds:jtds:1.3.1")
  testImplementation ("com.microsoft.sqlserver:mssql-jdbc:$mssqlDriverVersion")
  testImplementation ("mysql:mysql-connector-java:$mysqlDriverVersion")

  antSql ("net.sourceforge.jtds:jtds:1.3.1")
  antSql ("com.microsoft.sqlserver:mssql-jdbc:$mssqlDriverVersion")
  antSql ("mysql:mysql-connector-java:$mysqlDriverVersion")

  annotationProcessor project(':interlok-core-apt')
  testAnnotationProcessor project(':interlok-core-apt')

  javadoc project(':interlok-core-apt')

}


task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
  classifier = 'examples'
  from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.extensions.delombokTask
}

task generateUnitTestProperties {
  doLast {
    ant.property(file: project.projectDir.getCanonicalPath() + "/" + "build.properties")
    ant.property(file: testResourcesDir() + "/" + propertyTemplate(testResourcesDir(), "default-test.properties.template"))
    ant.property(file:  testResourcesDir() + "/" + propertyTemplate(testResourcesDir(), "unit-tests.properties.template"))
    ant.echoproperties(destfile:  testResourcesDir() + '/unit-tests.properties.resolved', prefix: "junit.")
    ant.replace(file:  testResourcesDir() + '/unit-tests.properties.resolved', token:"junit.", value: "")
    // Generate the security.properties file containing the private key for legacy things.
    ant.propertyfile(file: testResourcesDir() + "/security.properties", comment: "Legacy private key password") {
      entry(key: 'adaptris.privatekey.password', value: ant.properties['junit.security.password'])
    }
    ant.property(file: testResourcesDir() + "/" + propertyTemplate(testResourcesDir(), "security-test.properties.template"))
    ant.echoproperties(destfile: testResourcesDir() + '/security-test.properties.resolved', prefix: "junitsecurity.")
    ant.replace(file:  testResourcesDir() + '/security-test.properties.resolved', token:"junitsecurity.", value: "")
  }
}

task copyUnitTestProperties() {
  dependsOn(generateUnitTestProperties)
  doLast {
    delete file( testResourcesDir() + '/unit-tests.properties')
    ant.copy(file: testResourcesDir() + "/unit-tests.properties.resolved",
             tofile: testResourcesDir() + '/unit-tests.properties') {
      filterset {
         filter(token: "JAVA_HOME", value: System.getProperty("java.home").replaceAll("\\\\", "/"))
         filter(token: "BASE_DIR", value: project.projectDir.getCanonicalPath().replaceAll("\\\\", "/"))
         filter(token: "BUILD_DIR", value: project.buildDir.getCanonicalPath().replaceAll("\\\\", "/"))
      }
    }
  }
}

task copySecurityTestProperties() {
  dependsOn(generateUnitTestProperties)
  doLast {
    delete file( testResourcesDir() + '/security-test.properties')
    ant.copy(file: testResourcesDir() + "/security-test.properties.resolved",
             tofile: testResourcesDir() + '/security-test.properties') {
      filterset {
         filter(token: "JAVA_HOME", value: System.getProperty("java.home").replaceAll("\\\\", "/"))
         filter(token: "BASE_DIR", value: project.projectDir.getCanonicalPath().replaceAll("\\\\", "/"))
         filter(token: "BUILD_DIR", value: project.buildDir.getCanonicalPath().replaceAll("\\\\", "/"))
      }
    }
  }
}

task prepareForTests(dependsOn: [copySecurityTestProperties, copyUnitTestProperties]) {
  doLast {
    // unit tests are enabled.
    if ("true".equalsIgnoreCase(ant.properties["default.jdbc.storedproc.tests.enabled"])) {
        // Drop and re-create the database
      ant.sql(driver: ant.properties["default.jdbc.storedproc.driver"],
            url: ant.properties["default.jdbc.storedproc.url"]  + ant.properties["default.jdbc.storedproc.url.params"],
            userid: ant.properties["default.jdbc.storedproc.username"],
            password: ant.properties["default.jdbc.storedproc.password"],
            onerror: "continue", print: "false",
            classpath: configurations.antSql.asPath) {
        transaction("drop database " + ant.properties['default.jdbc.storedproc.database'])
        transaction("create database " + ant.properties['default.jdbc.storedproc.database'])
      }
      // Do the SQL.
      ant.sql(driver: ant.properties["default.jdbc.storedproc.driver"],
            url: ant.properties["default.jdbc.storedproc.url"] + ant.properties["default.jdbc.storedproc.database"] + ant.properties["default.jdbc.storedproc.url.params"],
            userid: ant.properties["default.jdbc.storedproc.username"],
            password: ant.properties["default.jdbc.storedproc.password"],
            onerror: "continue", print: "false", delimiter: "#",
            classpath: configurations.antSql.asPath) {
        transaction(src: testResourcesDir() + "/jdbc/" + ant.properties["default.jdbc.storedproc.vendor"] + "_create_data_for_procedures_tests.sql")
      }

      // Now rewrite unit-tests.properties so that we have the JDBC URL correct.
      if ("mysql".equalsIgnoreCase(ant.properties["default.jdbc.storedproc.vendor"])) {
        ant.propertyfile(file: testResourcesDir() + "/unit-tests.properties") {
          entry(key: 'jdbc.storedproc.url', value: ant.properties["default.jdbc.storedproc.url"]
              + ant.properties["default.jdbc.storedproc.database"]
              + ant.properties["default.jdbc.storedproc.url.params"]
              + "&user=" + ant.properties["default.jdbc.storedproc.username"]
              + "&password=" + ant.properties["default.jdbc.storedproc.password"])
        }
      } else if ("sqlserver".equalsIgnoreCase(ant.properties["default.jdbc.storedproc.vendor"])) {
        ant.propertyfile(file: testResourcesDir() + "/unit-tests.properties") {
          entry(key: 'jdbc.storedproc.url', value: ant.properties["default.jdbc.storedproc.url"]+ant.properties["default.jdbc.storedproc.database"] )
        }
      } else {
        ant.fail('Unsupported Jdbc Vendor')
      }
    }
  }
}


task deleteGeneratedFiles(type: Delete) {
  delete file(testResourcesDir() + '/unit-tests.properties')
  delete file(testResourcesDir() + '/unit-tests.properties.resolved')
  delete file(testResourcesDir() + '/security-test.properties')
  delete file(testResourcesDir() + '/security-test.properties.resolved')
  delete file(testResourcesDir() + '/security.properties')
  delete file('derby.log')
  delete 'activemq-data'
  delete delombokTargetDir
}


artifacts {
  archives javadocJar
  archives examplesJar
  archives sourcesJar
}

javadoc {
  configure(options) {
    options.linksOffline(interlokCommonJavadocs, interlokCommonsOfflineJavadocDir)

    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addStringOption "tagletpath", configurations.javadoc.asPath
    options.addBooleanOption "-no-module-directories", true
    options.addStringOption('Xdoclint:none', '-quiet')
    options.tags('apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:','implNote:a:Implementation Note:')
    options.
      group ("Base Adapter Configuration", ["com.adaptris.core.*" , "com.adaptris.core"]).
      group ("Standard Services", ["com.adaptris.core.services.*" , "com.adaptris.core.services"]).
      group ("Data Transformation and Validation", ["com.adaptris.core.transform.*" , "com.adaptris.core.transform", "com.adaptris.transform*", "com.adaptris.edi*"]).
      group ("Generic Filesystem", ["com.adaptris.core.fs.*" , "com.adaptris.core.fs","com.adaptris.fs", "com.adaptris.fs.*"]).
      group ("Connecting to JMS", ["com.adaptris.core.jms.*" , "com.adaptris.core.jms"]).
      group ("Email", ["com.adaptris.core.mail.*" , "com.adaptris.core.mail","com.adaptris.mail", "com.adaptris.mail.*"]).
      group ("FTP / SFTP", ["com.adaptris.core.ftp", "com.adaptris.core.ftp.*", "com.adaptris.filetransfer", "com.adaptris.filetransfer.*", "com.adaptris.ftp", "com.adaptris.ftp.*", "com.adaptris.sftp", "com.adaptris.sftp.*"]).
      group ("HTTP", ["com.adaptris.core.http", "com.adaptris.core.http.*", "com.adaptris.http", "com.adaptris.http.*"]).
      group ("Caching", ["com.adaptris.core.cache", "com.adaptris.core.cache.*", "com.adaptris.core.services.cache", "com.adaptris.core.services.cache.*"]).
      group ("JDBC", ["com.adaptris.core.jdbc", "com.adaptris.core.jdbc.*", "com.adaptris.core.services.jdbc", "com.adaptris.core.services.jdbc.*", "com.adaptris.jdbc", "com.adaptris.jdbc.*"]).
      group ("Security", ["com.adaptris.core.security", "com.adaptris.core.security.*", "com.adaptris.security", "com.adaptris.security.*"]).
      group ("Utility Classes", ["com.adaptris.util", "com.adaptris.util.*", "com.adaptris.core.util", "com.adaptris.core.util.*"]).
      group ("Adapter Management", ["com.adaptris.core.management", "com.adaptris.core.management.*", "com.adaptris.failover", "com.adaptris.core.runtime", "com.adadptris.core.runtime.*"])
    title= componentName
  }
}

test {
  include classesToTest
  maxHeapSize = "1G"
  maxParallelForks = maxThreads
  // listen to events in the test execution lifecycle
  beforeSuite { descriptor ->
     if (verboseTestLogging()) {
      logger.lifecycle("Running: " + descriptor)
     }
  }
  if (buildDetails.is_ci_pipeline()) {
    retry {
      maxRetries = 3
      maxFailures = 20
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
		asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.8.0+")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://github.com/adaptris/interlok/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}


delombok {
  target = delombokTargetDir
}

check.dependsOn jacocoTestReport
processTestResources.dependsOn prepareForTests
clean.dependsOn deleteGeneratedFiles
// javadoc.dependsOn offlinePackageList
