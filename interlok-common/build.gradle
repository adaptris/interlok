ext {
  componentName='Interlok Core/Common'
  componentDesc="Common components for Interlok"

  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  slf4jVersion = '2.0.1'
  log4j2Version = "2.18.0"
  mockitoVersion = '4.8.0'
}

// In this section you declare the dependencies for your production and test code
dependencies {

  api project(':interlok-core-apt')
  api ("com.thoughtworks.xstream:xstream:1.4.19")
  api ("commons-io:commons-io:2.11.0")
  api ("commons-net:commons-net:3.8.0")
  api ("commons-pool:commons-pool:1.6")
  api ("org.apache.commons:commons-lang3:3.12.0")
  api ("org.apache.commons:commons-pool2:2.11.1")

  api ("org.slf4j:slf4j-api:$slf4jVersion")
  api ("javax.servlet:javax.servlet-api:4.0.1")
  api ("org.eclipse.jetty.aggregate:jetty-all:9.4.48.v20220622")

  implementation "org.apache.logging.log4j:log4j-api:$log4j2Version", optional
  implementation "org.apache.logging.log4j:log4j-core:$log4j2Version", optional
  implementation "org.apache.logging.log4j:log4j-1.2-api:$log4j2Version", optional
  implementation "org.slf4j:jul-to-slf4j:$slf4jVersion", optional
  implementation "org.slf4j:jcl-over-slf4j:$slf4jVersion", optional
  implementation "org.slf4j:slf4j-reload4j:$slf4jVersion", optional

  annotationProcessor project(':interlok-core-apt')
  testAnnotationProcessor project(':interlok-core-apt')

  javadoc project(':interlok-core-apt')

  testImplementation ("junit:junit:4.13.2")
  testImplementation ("org.mockito:mockito-core:$mockitoVersion")
  testImplementation ("org.mockito:mockito-inline:$mockitoVersion")
  testImplementation ("org.awaitility:awaitility:4.2.0")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


javadoc {
  configure(options) {
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addBooleanOption "-no-module-directories", true
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption "tagletpath", configurations.javadoc.asPath
    options.tags('apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:','implNote:a:Implementation Note:')
    title= componentName
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
		asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.8.0+")
        properties.appendNode("license", "false")
        properties.appendNode("readme", "https://github.com/adaptris/interlok/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}


clean.dependsOn deleteGeneratedFiles
