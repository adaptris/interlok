ext {
  componentName='Interlok Core/Common'
  componentDesc="Common components for Interlok"

  delombokTargetDir = new File("${project.projectDir}/src/main/generated")
  slf4jVersion = '1.7.30'
  log4j2Version = "2.14.1"
  mockitoVersion = '3.9.0'
}

// In this section you declare the dependencies for your production and test code
dependencies {

  compile project(':interlok-core-apt')
  compile ("com.thoughtworks.xstream:xstream:1.4.16")
  compile ("commons-io:commons-io:2.8.0")
  compile ("commons-net:commons-net:3.8.0")
  compile ("commons-pool:commons-pool:1.6")
  compile ("org.apache.commons:commons-lang3:3.12.0")
  compile ("org.apache.commons:commons-pool2:2.9.0")

  compile ("org.slf4j:slf4j-api:$slf4jVersion")
  compile ("javax:javaee-api:8.0.1")

  compile ("org.slf4j:jcl-over-slf4j:$slf4jVersion")
  compile ("org.eclipse.jetty.aggregate:jetty-all:9.4.40.v20210413")
  compile "org.slf4j:jul-to-slf4j:$slf4jVersion", optional
  compile "org.apache.logging.log4j:log4j-core:$log4j2Version", optional
  compile "org.apache.logging.log4j:log4j-api:$log4j2Version", optional
  compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version", optional
  compile "org.apache.logging.log4j:log4j-1.2-api:$log4j2Version", optional
  compile ("javax.servlet:javax.servlet-api:4.0.1")

  annotationProcessor project(':interlok-core-apt')
  testAnnotationProcessor project(':interlok-core-apt')

  javadoc project(':interlok-core-apt')

  testCompile ("junit:junit:4.13.2")
  testCompile ("org.mockito:mockito-core:$mockitoVersion")
  testCompile ("org.mockito:mockito-inline:$mockitoVersion")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
  archives javadocJar
}

javadoc {
  configure(options) {
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addBooleanOption "-no-module-directories", true
    options.addStringOption "tagletpath", configurations.javadoc.asPath
    options.tags('apiNote:a:API Note:', 'implSpec:a:Implementation Requirements:','implNote:a:Implementation Note:')
    title= componentName
  }
}

delombok {
  target = delombokTargetDir
}

task deleteGeneratedFiles(type: Delete) {
  delete delombokTargetDir
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: mavenPublishUrl) {
        authentication(userName: repoUsername, password: repoPassword)
      }
      configuration = configurations.mavenDeploy
      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.8.0+")
        properties.appendNode("license", "false")
        properties.appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        properties.appendNode("readme", "https://github.com/adaptris/interlok/raw/develop/README.md")
        properties.appendNode("repository", "https://github.com/adaptris/interlok")
      }

    }
  }
}

publish.dependsOn uploadArchives
clean.dependsOn deleteGeneratedFiles
